plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'de.wellnerbou'
version = '3.3'

description = """git-changelog"""

mainClassName = "de.wellnerbou.gitchangelog.app.GitChangelog"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

// user and key configuration for bintray deployment:
// ./gradlew bintrayUpload -PpublishUser=<USER> -PpublishKey=<KEY>
if (!project.hasProperty("publishUser")) {
    ext.publishUser = ""
}
if (!project.hasProperty("publishKey")) {
    ext.publishKey = ""
}

run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'de.wellnerbou'
            artifactId 'git-changelog'
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

dependencies {
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.7.0.202003110725-r'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    testCompile "junit:junit:4.13"
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.16.1'
}

bintray {
    user = "$publishUser"
    key = "$publishKey"

    pkg {
        userOrg = 'paulwellnerbou'
        repo = 'maven'
        name = 'git-changelog'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/paulwellnerbou/git-changelog'
    }

    publications = ['MyPublication']

    dryRun = false
    publish = true
}
